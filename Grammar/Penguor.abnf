;
; Penguor Language
; ----------------
;
; (c) Carl Schierig 2019-2020
;
; abnf grammar according to https://tools.ietf.org/html/rfc2234

program = *declaration EOF

declaration = ( [ accessMod ] *nonAccessMod ( sysDecl / dataDecl / typeDecl / functionDecl / varDecl / libDecl ) )
            / usingDecl
            / compilerStmt

usingDecl    = "using" call ENDING
sysDecl      = "system" IDENTIFIER [ "<" call ] blockDecl
dataDecl     = "data" IDENTIFIER [ "<" call ] blockDecl
typeDecl     = "type" IDENTIFIER [ "<" call ] blockDecl
functionDecl = varExpr "(" [ parameters ] ")" declContent
varDecl      = varExpr [ "=" conditionalOrExpr ] ENDING
libDecl      = "library" IDENTIFIER *( "." IDENTIFIER ) blockDecl

; stmtblockDecl and blockDecl are both represented by a BlockDecl in PenguorCS, 
; with the difference that one allows DeclStmts
stmtblockDecl = "{" *( declaration / statement ) "}"
blockDecl     = "{" *declaration "}"
declContent   = stmtblockDecl
              / ":" statement

statement = compilerStmt
          / blockStmt
          / varStmt
          / ifStmt
          / whileStmt
          / forStmt
          / doStmt
          / switchStmt
          / expressionStmt

compilerStmt = "#" compilerDirective ENDING
compilerDirective = "safety" ( 0 / 1 / 2 )

blockStmt      = "{" *statement "}"

; the difference between a varStmt and a varDecl
; is that the varDecl is only called from declaration otherwise
; the varStmt may not have any access modifiers
varStmt        = varDecl

ifStmt         = "if" "(" expression ")" statement *elif [ "else" statement ]
elif           = "elif" "(" expression ")" statement

whileStmt      = "while" "(" expression ")" statement 
forStmt        = "for" "(" varExpr ":" call ")" statement
doStmt         = "do" statement "while" "(" expression ")" ";"
switchStmt     = "switch" "(" call ")" "{" 1*( caseStmt ) [ "default" ":" statement ] "}"
caseStmt       = "case" expression ":" statement

returnStmt     = "return" [ expression ] ENDING

expressionStmt = expression ENDING


expression     = assignmentExpr

assignmentExpr     = conditionalOrExpr [ ( "=" / "+=" / "-=" / "*=" / "/=" / "%=" / "<<=" / ">>=" / "&=" / "|=" / "^=" ) conditionalOrExpr ]
conditionalOrExpr  = conditionalXorExpr [ "||" conditionalOrExpr ]
conditionalXorExpr = conditionalAndExpr [ "^^" conditionalXorExpr ]
conditionalAndExpr = bitwiseOrExpr [ "&&" conditionalAndExpr ]
bitwiseOrExpr      = bitwiseXorExpr [ "|" bitwiseOrExpr ]
bitwiseXorExpr     = bitwiseAndExpr [ "^" bitwiseXorExpr ]
bitwiseAndExpr     = equalityExpr [ "&" bitwiseAndExpr ]
equalityExpr       = relationExpr [ ( "==" / "!=" ) equalityExpr ]
relationExpr       = bitshiftExpr [ ( "<" /  ">" / "<=" / ">=" ) relationExpr ]
bitshiftExpr       = additionExpr [ ( "<<" / ">>" ) bitshiftExpr ]
additionExpr       = multiplicationExpr [ ( "+" / "-" ) additionExpr ]
multiplicationExpr = unaryExpr [ ( "*" / "/" / "%" ) multiplicationExpr ]
unaryExpr          = [ "!" / "+" / "-" / "~" / "++" / "--" ] base / groupingExpr
base           = keyword / NUM / STRING / call

call           = IDENTIFIER [ ( "." call ) / ( "++" / "--" ) / ( "[" "]" ) ]
               / callFunction [ "." call ]
callFunction = IDENTIFIER "(" [ arguments ] ")"

keyword = "true" / "false" / "null"
varExpr = call IDENTIFIER

groupingExpr       = "(" expression ")"

parameters = varExpr *( "," varExpr )
arguments  = expression *( "," expression )

accessMod = "public" / "private" / "protected" / "restricted"
nonAccessMod = [ "static" / "dynamic" ] [ "abstract" ] [ "const" ]

; Regular grammar

IDENTIFIER = ( ALPHA / %x95 ) *( ALPHA / DIGIT / %x95 ) ; %x95 = '_'

NUM = 1*DIGIT [ "." DIGIT+ ]
    / *DIGIT "." 1*DIGIT

STRING = DQUOTE *(CHAR) DQUOTE

ENDING = ";" / NEWLINE
NEWLINE = LF / CRLF