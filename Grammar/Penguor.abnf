; 
; Penguor Language
; ----------------
;
; (c) Carl Schierig 2020
;
; abnf grammar according to https://tools.ietf.org/html/rfc2234

program = declaration* EOF

declaration = sysDec
            / contDec
            / dtypeDec
            / varDec
            / functionDec
            / libDec
            / statement

sysDec   = [ accessMod ] *nonAccessMod "system" IDENTIFIER [ "<" IDENTIFIER ] blockStmt
contDec  = [ accessMod ] *nonAccessMod "container" IDENTIFIER [ "<" IDENTIFIER ] blockStmt
dtypeDec = [ accessMod ] *nonAccessMod "datatype" IDENTIFIER [ "<" IDENTIFIER ] blockStmt
varDec      = [ accessMod ] *nonAccessMod IDENTIFIER IDENTIFIER [ "=" expression ] [";"] 
functionDec = [ accessMod ] *nonAccessMod IDENTIFIER IDENTIFIER "(" [ parameters ] ")" blockStmt
libDec      = "library" IDENTIFIER blockStmt


statement = preProcessorStmt
          / blockStmt
          / ifStmt
          / whileStmt
          / forStmt
          / doStmt
          / switchStmt
          / expressionStmt

preProcessorStmt = "#" ppDirective CRLF
ppDirective = "include" IDENTIFIER
             / "from" IDENTIFIER "include" IDENTIFIER
             / "safety" ( 0 / 1 / 2 )

blockStmt      = "{" *declaration "}"

ifStmt         = "if" "(" expression ")" "{" 1*statement "}" *elif [ else "{" 1*statement "}" ]
elif           = "elif" "(" expression ")" "{" 1*statement "}"

whileStmt      = "while" "(" expression ")" "{" 1*statement "}"
forStmt        = "for" "(" IDENTIFIER ":" expression ")" "{" 1*statement "}"
doStmt         = "do" "{" 1*statement "}" "while" "(" expression ")" ";"
switchStmt     = "switch" "(" IDENTIFIER ")" "{" 1*( caseStmt ) [ "default" ":" "{" 1*statement "}" ] "}" ; improve grammar & parser to allow for
caseStmt       = "case" expression ":" "{" *statement "}"

expressionStmt = expression ";"



expression     = assignment

assignment     = logicOr [ ( "=" / "+=" / "-=" / "*=" / "/=" ) logicOr ]
logicOr        = logicXor [ "||" logicOr ]
logicXor       = logicAnd [ "^^" logicXor ]
logicAnd       = bitwiseOr [ "&&" logicAnd ]
bitwiseOr      = bitwiseXor [ "|" bitwiseOr ]
bitwiseXor     = bitwiseAnd [ "^" bitwiseXor ]
bitwiseAnd     = equality [ "&" bitwiseAnd ]
equality       = relation [ ( "==" / "!=" ) equality ]
relation       = addition [ ( "<" /  ">" / "<=" / ">=" ) relation ]
addition       = multiplication [ ( "+" / "-" ) addition ]
multiplication = unary [ ( "*" / "/" ) multiplication ]
unary          = [ "!" / "-" / "~" ] unary / call / grouping
call           = IDENTIFIER [ "." call ]
               / callFunction [ "." call ]
               [ base ]

callFunction = IDENTIFIER "(" [ arguments ] ")"

base           = IDENTIFIER / NUM / STRING / "true" / "false" / "null"

grouping       = "(" expression ")"

parameters = varDec *( "," varDec )
arguments  = expression *( "," expression )

accessMod = "public" / "private" / "protected" / "restricted"
nonAccessMod = "static" / "dynamic" / "abstract" / "const"

; Regular grammar

IDENTIFIER = ( ALPHA / %x95 ) *( ALPHA / DIGIT / %x95 )

NUM = 1*DIGIT [ "." DIGIT+ ]
    / *DIGIT "." 1*DIGIT

STRING = DQUOTE *(CHAR) DQUOTE